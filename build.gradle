import org.apache.tools.ant.filters.ConcatFilter

plugins {
    id "com.gradle.build-scan" version "2.0.2"
    id "org.gradle.guides.getting-started" version "0.15.5"
    id "org.gradle.guides.ci.travis" version "0.15.5"
}

guide {
    repoPath 'gradle-guides/building-java-libraries'
}

asciidoctor {
    attributes 'gradle-outdir' : "${gradleRunner.reportsDir.absolutePath}"
}

gradleRunner {

    [
        [name: 'groovy-dsl', scriptExtension: '.gradle', initArguments: [], quote: '\''],
        [name: 'kotlin-dsl', scriptExtension: '.gradle.kts', initArguments: ['--dsl', 'kotlin'], quote: '"']
    ].each { sample ->

        ext {
            buildType = 'java-library'
            projectName = 'jvm-library'

            cleanBuildOutput = { stepName, info ->
                File out = new File(getStepReportDir(stepName), 'out.txt')
                new File(info.reportDir, 'out.txt').withWriter { w ->
                    out.filterLine { line ->
                        !(line =~ /^(Download|Cleaned up|Total time)/)
                    }.writeTo(w)
                }
            }

        }

        step "${sample.name}-initProject", ['init', '--type', buildType, '--package', 'guidesamples', '--project-name', projectName] + sample.initArguments

        step "${sample.name}-initOutput", cleanBuildOutput.curry("${sample.name}-initProject")

        step "${sample.name}patchSettings", { info ->
            new File(workingDir, "settings${sample.scriptExtension}").text = "rootProject.name = ${sample.quote}${project.name}${sample.quote}\n"
        }

        step "${sample.name}-buildProject", 'build'

        step "${sample.name}-buildOutput", cleanBuildOutput.curry("${sample.name}-buildProject")

        step "${sample.name}-addVersion", {
            File buildPrefix = project.file("contents/${sample.name}/build-prefix.txt")
            File editBuildFile = new File(workingDir, "build${sample.scriptExtension}")
            String editBuildFileText = editBuildFile.text
            File buildPostfix = project.file("contents/${sample.name}/build-postfix.txt")
            editBuildFile.withWriter { Writer writer ->
                writer.println buildPrefix.text
                writer.println editBuildFileText
                writer.println buildPostfix.text
            }
        }

        step "${sample.name}-updateJar", 'jar'

        step "${sample.name}-unpackJar", { info ->
            copy {
                from(zipTree(new File(workingDir, 'build/libs/building-java-libraries-0.1.0.jar'))) {
                    include 'META-INF/MANIFEST.MF'
                }
                into info.reportDir
            }
        }

        step "${sample.name}-editLibrary", {
            File editLibrary = new File(workingDir, 'src/main/java/guidesamples/Library.java')
            editLibrary.text = "${file('contents/javadoc-prefix.txt').text}${editLibrary.text}"
        }

        step "${sample.name}-docs", 'javadoc'

        step "${sample.name}-docOutput", cleanBuildOutput.curry("${sample.name}-docs")

        step "${sample.name}-files", { info ->
            copy {
                from workingDir
                into info.reportDir

                exclude '.gradle/**'
                exclude 'build/**'

                filesMatching(["build${sample.scriptExtension}".toString(), '**/*.java']) {
                    filter { line ->
                        if (line =~ /^(\/\*)|(\s*\*)|(\/\/)/
                            && !line.startsWith('// tag::')
                            && !line.startsWith('// end::')
                            && !line.startsWith("import")) {
                            null
                        } else {
                            line
                        }
                    }
                }

                filesMatching(["settings${sample.scriptExtension}".toString()]) {
                    filter { line ->
                        if (line =~ /^rootProject/) {
                            "${line} // <1>"
                        } else {
                            null
                        }
                    }
                }

                filesMatching(["build${sample.scriptExtension}".toString()]) {
                    filter { line ->
                        if (line =~ /\s*jcenter/) {
                            "${line} // <1>"
                        } else if (line =~ /\s*api.+commons-math/) {
                            "${line} // <2>"
                        } else if (line =~ /\s*implementation.+guava/) {
                            "${line} // <3>"
                        } else if (line =~ /\s*testImplementation.+junit/) {
                            "${line} // <4>"
                        } else {
                            line
                        }
                    }
                }

                filesMatching(['src/main/java/Library.java']) {
                    filter ConcatFilter, prepend: file('contents/javadoc-prefix.txt'),
                           append: file('contents/javadoc-postfix.txt')
                }
            }

        }

        step "${sample.name}-working-copy-cleanup", {
            delete getWorkingDir()
        }
    }
}

buildScan {
    setTermsOfServiceUrl("https://gradle.com/terms-of-service")
    setTermsOfServiceAgree("yes")
    if (System.getenv("CI") != null) {
        publishAlways()
        tag("CI")
    }
}

travis {
    encryptedVariables.add "Bfia7moUmGHZ3lyYG35jE7loP4WzPBp5u3gmPAM/N2ewWobH4xas1c5BLVVNZ5GV987XnVdQafePnPdMCUyI3uT404RY0OwY3TXk55ESiUDprdIWDu4rl7oAKPBmMGlhnyIV+kLpZgtY2RvO4r0UD3Cyfrio8l4NHQlHRoBu9h4uJvvfAue6j3n4kVcD0buSA+zstH0eXGgXTJA91x+YwCKQBkmYzocCIjkDWwjPE7j4DrgrhbpflP23uemVdkAA3vpZ02M5fS+K/93mu3kI4lPFJO2nVs9SY0EZQRPobxKefpsdcUDuq/3M+GVYhFr/iX6OsESDHy8pJpPDo0XR/rNkYKd2Hvym/o5gJbc4ZMJ0if7Z9NUS5A2t0wgJiHMRMzpSBQ0BO82G3Vu5zdFpSt/+Ik+kM+kv+XBV1j0p3uzjcBPa0pZlKR1KxlwjW3WFpEk+JGJK5VWHzaYT4JQTzQRMSIpFFba7MD9w/YVgC55E6r2ZXbZkjoVy3sTH6ycS9w9K6qc+uxCvOGC5QFSFJrzCCC/4t4asSRt+2TxZVXJ954D9sU+wE32MgnrWeI04M6XP1Q9cmMImdZpGhz0BfewmNqt8MAPeiN+1ZMxAx0PydXgYOCOV7+27Yi7K91L2DfD9MV78X7xyZYojaiyP5qXB9PFqXCNuEyTVD5QYsqg="
}
